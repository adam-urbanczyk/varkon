!*******************************************************

 GLOBAL GEOMETRY MODULE igs_in(
   STRING filnam*132 >"t1619");

!*      Läser IGES-fil.
!*
!*      In: filnam = t395 ="Vad heter IGES-filen ?";
!*
!*      (C)microform ab 1997-12-22 J. Kjellander
!*
!*      2004-07-20 eolsize, J.Kjellander
!*
!*******************************************************

   STRING path*132,rad*132;
   INT    status,ds_ptr,ps_ptr,eolsize;
   FILE   infil,logfil;

 BEGINMODULE

!*
!***Initiering.
!*

!*
!***Fixa fullständig filpath
!*
   part(#1,igs_fnam(filnam,path,status):SAVE=0);
!*
!***Går den att öppna ?
!*
   if status = -1 then
     exit(get_tstr(397)+path);          ! t397 = DXF-filen finns ej
   endif;
!*
!***Öppna för direktaccsess.
!*
   open(infil,"U",path);
   psh_pmt("Reading:"+path);
!*
!***Öppna logfil.
!*
   open(logfil,"W",act_jobdir()+"IGES.LOG");
   outstr(logfil,"IGES-logfile generated by module igs_in.");
   outlin(logfil);
   outstr(logfil,"Jobname:"+act_jobnam());
   outlin(logfil);
   outstr(logfil,"IGES-filename:"+filnam);
   outlin(logfil);
!*
!***Start sektion.
!*
   part(#2,igs_ssec(infil,logfil,eolsize,status):SAVE=0);
   if status < 0 then
     goto end;
   endif;
!*
!***Global sektion.
!*
   part(#3,igs_gsec(infil,logfil,eolsize,status):SAVE=0);
   if status < 0 then
     goto end;
   endif;
!*
!***Directory Entry sektion Pass 1.
!*
   ds_ptr:=fpos(infil);
   part(#4,igs_dsec(infil,logfil,eolsize,status):SAVE=0);
   if status < 0 then
     goto end;
   endif;
!*
!***Directory Entry sektion Pass 2.
!*
   ps_ptr:=fpos(infil);
   seek(infil,ds_ptr);
   part(#5,igs_ents(infil,logfil,ps_ptr,eolsize,status):SAVE=0);
   if status < 0 then
     goto end;
   endif;
!*
!***Slut.
!*
end:
   pop_pmt();
   close(infil);
   close(logfil);
!*
!***Lista logfil.
!*
   open(logfil,"R",act_jobdir()+"IGES.LOG");
   lst_ini("");

logloop:
   rad:=inlin(logfil);
   if iostat(logfil) = 0 then
     lst_lin(rad);
     goto logloop;
   else
     close(logfil);
     lst_exi();
   endif; 


 ENDMODULE

!*******************************************************




