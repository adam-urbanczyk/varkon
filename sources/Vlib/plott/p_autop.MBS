!*******************************************************

 MACRO MODULE p_autop();

!*      Skapar plotfil av hela skärmfönstret och 
!*      skickar till en plotter med samma skala.
!*
!*      (C)microform ab 20/9/95 J. Kjellander
!*
!*      1996-01-30 -pf, J.Kjellander
!*      1996-03-07 WIN32, J.Kjellander
!*      1997-01-06 OS-cmd via fil, J.Kjellander
!*      1997-03-24 DLL, J.Kjellander
!*
!*******************************************************

 STRING namn*20,alt*3,pnam*20,plp(5)*132,tmpdir*132,
        plotfil*132,param*132,actparams*132,dest*132,
        add*132,pennfil*132,driver*132;
 FLOAT  skala,xmin,xmax,ymin,ymax,trf(4,4),persp;
 INT    status,format;

 BEGINMODULE

!*
!***Läs aktuell konfigurationsfil.
!*
   alt:="-";
   part(#1,p_getplt(alt,pnam,plp,status):SAVE=0);
   if status < 0 then exit(); endif;
!*
!***Drivrutin, tex. "epson <" eller "msw32"
!*
   driver:=plp(1);
!*
!***Destination, tex. "| lpr -Ppp1" eller "-PCanon FAX B320/340"
!*
   dest:=plp(2);
!*
!***Konfigurationsfilens parametrar.
!*
   param:=plp(4);
!*
!***Skala.
!*
   get_view(0,namn,skala,xmin,ymin,xmax,ymax,trf,persp);
!*
!***Stående eller liggande.
!*
   part(#2,p_getsl(format));
   if format = -1 then
     exit();
   endif;

   psh_pmt(get_tstr(1618)); ! Plottning startad
!*
!***Skapa plotfil. Origo i nedre vänstra hörnet.
!*
   if act_ostype() = "UNIX" then
     tmpdir:=get_environment("VARKON_TMP")+"/";
   elif act_ostype() = "WIN32" then
     tmpdir:=get_environment("VARKON_TMP")+"\";
   endif;

   plotfil:=tmpdir+unique_filename()+".PLT";
   plot_win(vec(xmin,ymin),vec(xmax,ymax),plotfil);
!*
!***Skala.
!*
   actparams:="-s"+str(skala,-1,6);
!*
!***Vridning och placering.
!*
   if   format =  1 then
     add:="";
   elif format =  2 then
     add:=" -v90 -x-210";
   endif;

   actparams:=actparams+add;
!*
!***Hastighet. Default för plottrar = 0.6. Övriga inget.
!***För skrivare = upplösning i X-led.
!*
   if finds(param,"-h") > 0 then
     add:=substr(param,finds(param,"-h")+2);
     add:=substr(add,1,finds(add," ")-1);
     add:=" -h"+add;
   else
     if status = 0 then
       add:=" -h0.6";
     else
       add:="";
     endif;
   endif;

   actparams:=actparams+add;
!*
!***Antal pinnar för matrisskrivare.
!*
   if finds(param,"-p24") > 0 then
     add:=" -p24";
   elif finds(param,"-p8") > 0 then
     add:=" -p8";
   else
     add:="";
   endif;

   actparams:=actparams+add;
!*
!***Pappersbredd.
!*
   if finds(param,"-w") > 0 then
     add:=substr(param,finds(param,"-w")+2);
     add:=substr(add,1,finds(add," ")-1);
     add:=" -w"+add;
   else
     add:="";
   endif;

   actparams:=actparams+add;
!*
!***Papperslängd.
!*
   if finds(param,"-l") > 0 then
     add:=substr(param,finds(param,"-l")+2);
     add:=substr(add,1,finds(add," ")-1);
     add:=" -l"+add;
   else
     add:="";
   endif;

   actparams:=actparams+add;
!*
!***Eventuellt formfeed.
!*
   if finds(param,"-FF") > 0 then
     add:=" -FF";
   else
     add:="";
   endif;

   actparams:=actparams+add;
!*
!***Eventuell pennfil.
!*
   if finds(param,"-pf") > 0 then
     part(#3,p_trenv(plp(5),pennfil));

     if pennfil = "act_job" then
       pennfil:=act_jobdir()+act_jobnam()+".PEN";
     endif;

     if test_file(pennfil,"R") = 1 then
       if act_ostype() = "UNIX" then
         add:=" -pf" + plp(5);
       else
         add:=" -pf" + pennfil;
       endif;
     else
       add:="";
     endif;
   else
     add:="";
   endif;

   actparams:=actparams+add;
!*
!***Starta plottning.
!*
   if act_ostype() = "UNIX" then
     part(#4,p_unix(plotfil,driver,dest,actparams,1));
   elif act_ostype() ="WIN32" then
     part(#5,p_win95(plotfil,driver,dest,actparams,1));
   endif;
!*
!***Slut.
!*
   pop_pmt();

 ENDMODULE
   
!*******************************************************
