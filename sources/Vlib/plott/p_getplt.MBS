!*******************************************************

 macro module p_getplt(
 VAR STRING alt*3;
 VAR STRING pnam*20;
 VAR STRING plp(5)*132;
 VAR INT    flg);

!*      Hämtar plotter.
!*
!*      In:  Inget.
!*
!*      Ut:  alt = Vald plotter och filnamn till conf.filen.
!*           pnam= Plotterns namn.
!*           plp(1) = Plotterprogram.
!*           plp(2) = Path till porten.
!*           plp(3) = Fil att plotta.
!*           plp(4) = Parametrar.
!*           plp(5) = Parametern pf = Pennfil.
!*           flg =  0 = Plotter.
!*                  1 = Skrivare.
!*                 -1 = Abvryt.
!*
!*      (C)microform ab 1/2/91 R. Svedin
!*
!*      1996-01-30 Postscript, J.Kjellander
!*      1996-06-07 Engelska, J.Kjellander
!*
!*******************************************************

 STRING s1*3;

 beginmodule

!*
!***Om plotter inte är vald i en tidigare part välj en här.
!*
   if alt = "-" then
     get_men(135,s1);
!*
!***Finns det fler än en plotter att välja på.
!*
     if finds(s1,"a") = 1 then
       alt:=s1;
!*
!***Välj plotter.
!*
     else
       psh_men(135);
       alt:=get_alt("");
       pop_men();
     endif;
   endif;
!*
!***Avbryt.
!*
   if   alt = "" then 
     flg:=-1; 
     exit();
!*
!***Fel.
!*
   elif substr(alt,1,1) <> "a" then
     exit("Syntax error in m135 = "+alt);
   endif;
!*
!***Läs in conf. filen
!*
   part(#1,p_redcnf(alt,plp):SAVE=0);

   if   finds(plp(1),"be16")       > 0  then flg:=0; pnam:="OCE";
   elif finds(plp(1),"cc1043")     > 0  then flg:=0; pnam:="CALCOMP";
   elif finds(plp(1),"hi29")       > 0  then flg:=0; pnam:="HI29";
   elif finds(plp(1),"hp7475")     > 0  then flg:=0; pnam:="HP 7475";
   elif finds(plp(1),"sweetp")     > 0  then flg:=0; pnam:="SWEETPEE";
   elif finds(plp(1),"flash4p")    > 0  then flg:=0; pnam:="FLASHSCAN";
   elif finds(plp(1),"gerber")     > 0  then flg:=0; pnam:="GERBER";
   elif finds(plp(1),"t4010")      > 0  then flg:=0; pnam:="T4010";
   elif finds(plp(1),"hpcl5")      > 0  then flg:=0; pnam:="HP Laser";
   elif finds(plp(1),"hpcl5_a3")   > 0  then flg:=0; pnam:="HP A3-Laser";
   elif finds(plp(1),"ln03")       > 0  then flg:=0; pnam:="LN03";
   elif finds(plp(1),"epson")      > 0  then flg:=1; pnam:="EPSON";
   elif finds(plp(1),"ft4510")     > 0  then flg:=1; pnam:="FACIT";
   elif finds(plp(1),"ibmpro")     > 0  then flg:=1; pnam:="IBM Pro";
   elif finds(plp(1),"postscript") > 0  then flg:=0; pnam:="POSTSCRIPT";
   elif finds(plp(1),"msw32")      > 0  then flg:=1; pnam:="WIN32";
   else
     flg:=1; pnam:="Unknown_plotter";
   endif;

 endmodule
   
!*******************************************************
