!*******************************************************

 MACRO MODULE p_plott();

!*      Huvudrutin för manuell plottning.
!*
!*      (C)microform ab 1997-11-27 J.Kjellander
!*
!*******************************************************

 STRING alt*3,plt*3,pnam*20,plp(5)*132,plotfil*132,driver*132,
        dest*132,param*132,pennfil*132;
 INT    status;

 BEGINMODULE

!*
!***Läs aktuell konfigurationsfil.
!*
   plt:="-";
   part(#1,p_getplt(plt,pnam,plp,status):SAVE=0);
   if status < 0 then exit(); endif;
!*
!***Plotfil.
!*
   if finds(plp(3),"act_job") > 0 then
     plotfil:=inpmt(get_tstr(138),act_jobdir()+act_jobnam()+".PLT",132);
   else
     plotfil:=inpmt(get_tstr(138),plp(3),132);
   endif;

   if plotfil = "" then exit(); endif;

   if test_file(plotfil,"R") = 0 then
     exit("Can't find:"+plotfil);
   endif;
!*
!***Drivrutin, tex. "epson <" eller "msw32"
!*
   driver:=plp(1);
!*
!***Destination, tex. "| lpr -Ppp1" eller "-PCanon FAX B320/340"
!*
   dest:=plp(2);
!*
!***Plot-parametrar.
!*
   param:=plp(4);
!*
!***Välj ur menyn.
!*
loop:
   psh_men(138);
   alt:=get_alt("");
   pop_men();
!*
!***Ändra inställningar.
!*
   if alt = "a1" then
    part(#3,p_chgppa(param)); 
    plp(4):=param;
    part(#4,p_savcnf(plt,plp));
    goto loop;
!*
!***Starta plottning.
!*
   elif alt = "a2" then
!*
!*
!***Ett litet meddelande.
!*
     psh_pmt(get_tstr(1618)); ! Plottning startad
!*
!***Eventuell pennfil.
!*
     if finds(param,"-pf") > 0 then
       part(#2,p_trenv(plp(5),pennfil));

       if pennfil = "act_job" then
         pennfil:=act_jobdir()+act_jobnam()+".PEN";
       endif;

       if test_file(pennfil,"R") = 1 then
         if act_ostype() = "UNIX" then
           param:=param +  plp(5);
         else
           param:=param + pennfil;
         endif;
       else
         pop_pmt();
         exit("Cant't find penfile:"+pennfil);
       endif;
     endif;
!*
!***Starta plottning.
!*
     if act_ostype() = "UNIX" then
       part(#5,p_unix(plotfil,driver,dest,param,2));
     elif act_ostype() ="WIN32" then
       part(#6,p_win95(plotfil,driver,dest,param,2));
     endif;
     pop_pmt();
   else
     exit();
   endif;
!*
!***Slut.
!*

 ENDMODULE
   
!*******************************************************
